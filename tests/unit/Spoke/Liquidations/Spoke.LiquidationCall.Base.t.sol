// SPDX-License-Identifier: UNLICENSED
// Copyright (c) 2025 Aave Labs
pragma solidity ^0.8.0;

import 'tests/unit/libraries/LiquidationLogic/LiquidationLogic.Base.t.sol';

contract SpokeLiquidationCallBaseTest is LiquidationLogicBaseTest {
  using SafeCast for *;
  using PercentageMath for *;
  using WadRayMath for *;
  using KeyValueList for KeyValueList.List;
  using MathUtils for uint256;

  uint256 internal constant MAX_AMOUNT_IN_BASE_CURRENCY = 1_000_000_000e26; // 1 billion USD
  uint256 internal constant MIN_AMOUNT_IN_BASE_CURRENCY = 1e26; // 1 USD

  struct CheckedLiquidationCallParams {
    ISpoke spoke;
    uint256 collateralReserveId;
    uint256 debtReserveId;
    address user;
    uint256 debtToCover;
    address liquidator;
    bool isSolvent;
    bool receiveShares;
  }

  struct BalanceInfo {
    uint256 collateralErc20Balance;
    uint256 suppliedInSpoke;
    uint256 addedInHub;
    uint256 debtErc20Balance;
    uint256 borrowedFromSpoke;
    uint256 drawnFromHub;
  }

  struct AccountsInfo {
    ISpoke.UserAccountData userAccountData;
    BalanceInfo userBalanceInfo;
    BalanceInfo collateralHubBalanceInfo;
    BalanceInfo debtHubBalanceInfo;
    BalanceInfo liquidatorBalanceInfo;
    BalanceInfo collateralFeeReceiverBalanceInfo;
    BalanceInfo debtFeeReceiverBalanceInfo;
    BalanceInfo spokeBalanceInfo;
    uint256 userLastRiskPremium;
  }

  struct LiquidationMetadata {
    uint256 debtToTarget;
    uint256 collateralToLiquidate;
    uint256 collateralToLiquidator;
    uint256 collateralSharesToLiquidate;
    uint256 collateralSharesToLiquidator;
    uint256 debtToLiquidate;
    uint256 drawnSharesToLiquidate;
    uint256 premiumDebtRayToLiquidate;
    uint256 liquidationBonus;
    ISpoke.UserAccountData expectedUserAccountData;
    bool fullDebtReserveLiquidated;
    bool isCollateralAffectingUserHf;
    bool hasDeficit;
  }
  struct ExpectEventsAndCallsParams {
    uint256 userDrawnDebt;
    uint256 userPremiumDebt;
    uint256 drawnAmountToRestore;
    int256 realizedDelta;
    IHubBase.PremiumDelta premiumDelta;
    ISpoke.UserPosition userReservePosition;
    ISpoke.UserPosition userDebtPosition;
    IHub collateralHub;
    IHub debtHub;
    uint256 debtAssetId;
    uint256 collateralAssetId;
  }

  /// @notice Bound liquidation config to full range of possible values
  function _bound(
    ISpoke.LiquidationConfig memory liqConfig
  ) internal pure virtual returns (ISpoke.LiquidationConfig memory) {
    liqConfig.targetHealthFactor = bound(
      liqConfig.targetHealthFactor,
      HEALTH_FACTOR_LIQUIDATION_THRESHOLD,
      MAX_CLOSE_FACTOR
    ).toUint120();

    liqConfig.healthFactorForMaxBonus = bound(
      liqConfig.healthFactorForMaxBonus,
      0,
      HEALTH_FACTOR_LIQUIDATION_THRESHOLD - 1
    ).toUint64();

    liqConfig.liquidationBonusFactor = bound(
      liqConfig.liquidationBonusFactor,
      0,
      PercentageMath.PERCENTAGE_FACTOR
    ).toUint16();

    return liqConfig;
  }

  function _bound(
    ISpoke.DynamicReserveConfig memory dynConfig
  ) internal pure virtual returns (ISpoke.DynamicReserveConfig memory) {
    dynConfig.maxLiquidationBonus = bound(
      dynConfig.maxLiquidationBonus,
      MIN_LIQUIDATION_BONUS,
      MAX_LIQUIDATION_BONUS
    ).toUint32();
    dynConfig.collateralFactor = bound(
      dynConfig.collateralFactor,
      1,
      (PercentageMath.PERCENTAGE_FACTOR - 1).percentDivDown(dynConfig.maxLiquidationBonus)
    ).toUint16();
    return dynConfig;
  }

  function _boundAssume(
    ISpoke spoke,
    uint256 collateralReserveId,
    uint256 debtReserveId,
    address user,
    address liquidator
  ) internal virtual returns (uint256, uint256, address) {
    collateralReserveId = bound(collateralReserveId, 0, spoke.getReserveCount() - 1);
    debtReserveId = bound(debtReserveId, 0, spoke.getReserveCount() - 1);
    vm.assume(user != liquidator);
    assumeUnusedAddress(user);
    return (collateralReserveId, debtReserveId, user);
  }

  function _boundDebtToCoverNoDustRevert(
    ISpoke spoke,
    uint256 collateralReserveId,
    uint256 debtReserveId,
    address user,
    uint256 debtToCover,
    address liquidator
  ) internal virtual returns (uint256) {
    debtToCover = bound(
      debtToCover,
      _convertValueToAmount(spoke, debtReserveId, 1e26),
      MAX_SUPPLY_AMOUNT
    );

    LiquidationLogic.CalculateLiquidationAmountsParams
      memory params = _getCalculateLiquidationAmountsParams(
        spoke,
        collateralReserveId,
        debtReserveId,
        user,
        debtToCover
      );
    try liquidationLogicWrapper.calculateLiquidationAmounts(params) returns (
      LiquidationLogic.LiquidationAmounts memory
    ) {} catch {
      uint256 liquidationBonus = spoke.getLiquidationBonus(
        collateralReserveId,
        user,
        spoke.getUserAccountData(user).healthFactor
      );
      uint256 debtReserveBalance = params.drawnShares.rayMulUp(params.drawnIndex) +
        params.premiumDebtRay.fromRayUp();
      uint256 collateralReserveBalance = params.collateralReserveHub.previewRemoveByShares(
        params.collateralReserveAssetId,
        params.suppliedShares
      );
      debtToCover = bound(
        debtToCover,
        debtReserveBalance.min(
          _convertAssetAmount(
            spoke,
            collateralReserveId,
            collateralReserveBalance.percentDivUp(liquidationBonus),
            debtReserveId
          )
        ),
        MAX_SUPPLY_AMOUNT
      );
    }
    deal(spoke, debtReserveId, liquidator, debtToCover.percentMulUp(101_00));
    Utils.approve(spoke, debtReserveId, liquidator, debtToCover.percentMulUp(101_00));

    return debtToCover;
  }

  function _bound(
    ISpoke spoke,
    uint256[] memory reserveIds,
    uint256 reserveIdToExclude,
    uint256 maxLength
  ) internal view returns (bytes memory) {
    uint256[] memory boundedReserveIds = new uint256[](_min(reserveIds.length, maxLength));

    for (uint256 i = 0; i < boundedReserveIds.length; i++) {
      boundedReserveIds[i] = bound(reserveIds[i], 0, spoke.getReserveCount() - 1);
      if (boundedReserveIds[i] == reserveIdToExclude) {
        boundedReserveIds[i] = bound(boundedReserveIds[i] + 1, 0, spoke.getReserveCount() - 1);
      }
    }
    return abi.encode(boundedReserveIds);
  }

  function _getCalculateDebtToLiquidateParams(
    ISpoke spoke,
    uint256 collateralReserveId,
    uint256 debtReserveId,
    address user,
    uint256 debtToCover
  ) internal virtual returns (LiquidationLogic.CalculateDebtToLiquidateParams memory) {
    ISpoke.UserAccountData memory userAccountData = spoke.getUserAccountData(user);
    return
      LiquidationLogic.CalculateDebtToLiquidateParams({
        drawnShares: spoke.getUserPosition(debtReserveId, user).drawnShares,
        premiumDebtRay: _calculatePremiumDebtRay(spoke, debtReserveId, user),
        drawnIndex: _reserveDrawnIndex(spoke, debtReserveId),
        totalDebtValue: userAccountData.totalDebtValue,
        debtAssetPrice: IPriceOracle(spoke.ORACLE()).getReservePrice(debtReserveId),
        debtAssetDecimals: spoke.getReserve(debtReserveId).decimals,
        debtAssetUnit: 10 ** spoke.getReserve(debtReserveId).decimals,
        debtToCover: debtToCover,
        liquidationBonus: spoke.getLiquidationBonus(
          collateralReserveId,
          user,
          userAccountData.healthFactor
        ),
        collateralFactor: spoke
          .getDynamicReserveConfig(
            collateralReserveId,
            spoke.getUserPosition(collateralReserveId, user).dynamicConfigKey
          )
          .collateralFactor,
        healthFactor: userAccountData.healthFactor,
        targetHealthFactor: spoke.getLiquidationConfig().targetHealthFactor
      });
  }

  function _getCalculateDebtToTargetHealthFactorParams(
    ISpoke spoke,
    uint256 collateralReserveId,
    uint256 debtReserveId,
    address user
  ) internal virtual returns (LiquidationLogic.CalculateDebtToTargetHealthFactorParams memory) {
    ISpoke.UserAccountData memory userAccountData = spoke.getUserAccountData(user);
    return
      LiquidationLogic.CalculateDebtToTargetHealthFactorParams({
        totalDebtValue: userAccountData.totalDebtValue,
        debtAssetUnit: 10 ** spoke.getReserve(debtReserveId).decimals,
        debtAssetPrice: IPriceOracle(spoke.ORACLE()).getReservePrice(debtReserveId),
        collateralFactor: spoke
          .getDynamicReserveConfig(
            collateralReserveId,
            spoke.getUserPosition(collateralReserveId, user).dynamicConfigKey
          )
          .collateralFactor,
        liquidationBonus: spoke.getLiquidationBonus(
          collateralReserveId,
          user,
          userAccountData.healthFactor
        ),
        healthFactor: userAccountData.healthFactor,
        targetHealthFactor: spoke.getLiquidationConfig().targetHealthFactor
      });
  }

  function _getCalculateLiquidationAmountsParams(
    ISpoke spoke,
    uint256 collateralReserveId,
    uint256 debtReserveId,
    address user,
    uint256 debtToCover
  ) internal virtual returns (LiquidationLogic.CalculateLiquidationAmountsParams memory) {
    ISpoke.UserAccountData memory userAccountData = spoke.getUserAccountData(user);
    return
      LiquidationLogic.CalculateLiquidationAmountsParams({
        collateralReserveHub: _hub(spoke, collateralReserveId),
        collateralReserveAssetId: spoke.getReserve(collateralReserveId).assetId,
        suppliedShares: spoke.getUserPosition(collateralReserveId, user).suppliedShares,
        collateralAssetDecimals: spoke.getReserve(collateralReserveId).decimals,
        collateralAssetPrice: IPriceOracle(spoke.ORACLE()).getReservePrice(collateralReserveId),
        drawnShares: spoke.getUserPosition(debtReserveId, user).drawnShares,
        premiumDebtRay: _calculatePremiumDebtRay(spoke, debtReserveId, user),
        drawnIndex: _reserveDrawnIndex(spoke, debtReserveId),
        totalDebtValue: userAccountData.totalDebtValue,
        debtAssetDecimals: spoke.getReserve(debtReserveId).decimals,
        debtAssetPrice: IPriceOracle(spoke.ORACLE()).getReservePrice(debtReserveId),
        debtToCover: debtToCover,
        collateralFactor: spoke
          .getDynamicReserveConfig(
            collateralReserveId,
            spoke.getUserPosition(collateralReserveId, user).dynamicConfigKey
          )
          .collateralFactor,
        healthFactorForMaxBonus: spoke.getLiquidationConfig().healthFactorForMaxBonus,
        liquidationBonusFactor: spoke.getLiquidationConfig().liquidationBonusFactor,
        maxLiquidationBonus: spoke
          .getDynamicReserveConfig(
            collateralReserveId,
            spoke.getUserPosition(collateralReserveId, user).dynamicConfigKey
          )
          .maxLiquidationBonus,
        targetHealthFactor: spoke.getLiquidationConfig().targetHealthFactor,
        healthFactor: userAccountData.healthFactor,
        liquidationFee: spoke
          .getDynamicReserveConfig(
            collateralReserveId,
            spoke.getUserPosition(collateralReserveId, user).dynamicConfigKey
          )
          .liquidationFee
      });
  }

  function _makeUserLiquidatable(
    ISpoke spoke,
    address user,
    uint256 debtReserveId,
    uint256 newHealthFactor
  ) internal virtual {
    // add liquidity
    _openSupplyPosition(
      spoke,
      debtReserveId,
      _getRequiredDebtAmountForHf(spoke, user, debtReserveId, newHealthFactor)
    );
    // borrow to be at target health factor
    _borrowToBeAtHf(spoke, user, debtReserveId, newHealthFactor);
  }

  struct CalculateExpectedUserAccountDataParams {
    ISpoke spoke;
    address user;
    uint256 collateralReserveId;
    uint256 debtReserveId;
    uint256 collateralSharesToLiquidate;
    uint256 drawnSharesToLiquidate;
    uint256 premiumDebtRayToLiquidate;
    bool hasDeficit;
  }

  // calculate expected user account data after liquidation
  function _calculateExpectedUserAccountData(
    CalculateExpectedUserAccountDataParams memory params
  ) internal virtual returns (ISpoke.UserAccountData memory expectedUserAccountData) {
    KeyValueList.List memory list = KeyValueList.init(params.spoke.getReserveCount());

    for (uint256 reserveId = 0; reserveId < params.spoke.getReserveCount(); reserveId++) {
      if (!_isUsingAsCollateral(params.spoke, reserveId, params.user)) {
        continue;
      }

      uint256 collateralFactor = _getCollateralFactor(params.spoke, reserveId, params.user);
      if (collateralFactor == 0) {
        continue;
      }

      uint256 userSuppliedShares = params
        .spoke
        .getUserPosition(reserveId, params.user)
        .suppliedShares;
      if (params.collateralReserveId == reserveId) {
        userSuppliedShares -= params.collateralSharesToLiquidate;
      }
      if (userSuppliedShares == 0) {
        continue;
      }

      uint256 userSuppliedValue = _convertAmountToValue(
        params.spoke,
        reserveId,
        _hub(params.spoke, reserveId).previewRemoveByShares(
          params.spoke.getReserve(reserveId).assetId,
          userSuppliedShares
        )
      );
      list.add(
        expectedUserAccountData.activeCollateralCount++,
        _getCollateralRisk(params.spoke, reserveId),
        userSuppliedValue
      );
      expectedUserAccountData.totalCollateralValue += userSuppliedValue;
      expectedUserAccountData.avgCollateralFactor += collateralFactor * userSuppliedValue;
    }

    for (
      uint256 reserveId = 0;
      reserveId < params.spoke.getReserveCount() && !params.hasDeficit;
      reserveId++
    ) {
      if (!_isBorrowing(params.spoke, reserveId, params.user)) {
        continue;
      }

      uint256 userDrawnShares = params.spoke.getUserPosition(reserveId, params.user).drawnShares;
      uint256 userPremiumDebtRay = _calculatePremiumDebtRay(params.spoke, reserveId, params.user);
      if (params.debtReserveId == reserveId) {
        userDrawnShares -= params.drawnSharesToLiquidate.toUint120();
        userPremiumDebtRay -= params.premiumDebtRayToLiquidate;
      }
      if (userDrawnShares == 0) {
        continue;
      }
      expectedUserAccountData.borrowedCount++;
      expectedUserAccountData.totalDebtValue += _convertAmountToValue(
        params.spoke,
        reserveId,
        userDrawnShares.rayMulUp(_reserveDrawnIndex(params.spoke, reserveId)) +
          userPremiumDebtRay.fromRayUp()
      );
    }

    if (expectedUserAccountData.totalDebtValue > 0) {
      expectedUserAccountData.healthFactor = expectedUserAccountData
        .avgCollateralFactor
        .wadDivDown(expectedUserAccountData.totalDebtValue)
        .fromBpsDown();
    } else {
      expectedUserAccountData.healthFactor = type(uint256).max;
    }

    if (expectedUserAccountData.totalCollateralValue != 0) {
      expectedUserAccountData.avgCollateralFactor = expectedUserAccountData
        .avgCollateralFactor
        .wadDivDown(expectedUserAccountData.totalCollateralValue)
        .fromBpsDown();
    }
    list.sortByKey();

    uint256 remainingDebtToCover = expectedUserAccountData.totalDebtValue;
    for (uint256 i = 0; i < list.length() && remainingDebtToCover > 0; i++) {
      (uint256 collateralRisk, uint256 collateralValue) = list.get(i);
      expectedUserAccountData.riskPremium +=
        collateralRisk * _min(collateralValue, remainingDebtToCover);
      remainingDebtToCover -= _min(collateralValue, remainingDebtToCover);
    }

    expectedUserAccountData.riskPremium /= _max(
      1,
      _min(expectedUserAccountData.totalDebtValue, expectedUserAccountData.totalCollateralValue)
    );

    return expectedUserAccountData;
  }

  function _expectEventsAndCalls(
    CheckedLiquidationCallParams memory params,
    AccountsInfo memory /*accountsInfoBefore*/,
    LiquidationMetadata memory liquidationMetadata
  ) internal virtual {
    ExpectEventsAndCallsParams memory vars;

    vars.userDebtPosition = params.spoke.getUserPosition(params.debtReserveId, params.user);
    vars.collateralHub = _hub(params.spoke, params.collateralReserveId);
    vars.debtHub = _hub(params.spoke, params.debtReserveId);
    vars.debtAssetId = _reserveAssetId(params.spoke, params.debtReserveId);
    vars.collateralAssetId = _reserveAssetId(params.spoke, params.collateralReserveId);

    (vars.userDrawnDebt, vars.userPremiumDebt) = params.spoke.getUserDebt(
      params.debtReserveId,
      params.user
    );

    vars.drawnAmountToRestore = vars.debtHub.previewRestoreByShares(
      vars.debtAssetId,
      liquidationMetadata.drawnSharesToLiquidate
    );
    uint256 amountToRestore = vars.drawnAmountToRestore +
      liquidationMetadata.premiumDebtRayToLiquidate.fromRayUp();
    vars.premiumDelta = _getExpectedPremiumDeltaForRestore(
      params.spoke,
      params.user,
      params.debtReserveId,
      amountToRestore
    );

    vm.expectEmit(address(params.spoke));
    emit ISpokeBase.LiquidationCall({
      collateralReserveId: params.collateralReserveId,
      debtReserveId: params.debtReserveId,
      user: params.user,
      liquidator: params.liquidator,
      receiveShares: params.receiveShares,
      debtAmountRestored: amountToRestore,
      drawnSharesLiquidated: liquidationMetadata.drawnSharesToLiquidate,
      premiumDelta: vars.premiumDelta,
      collateralAmountRemoved: vars.collateralHub.previewRemoveByShares(
        vars.collateralAssetId,
        liquidationMetadata.collateralSharesToLiquidate
      ),
      collateralSharesLiquidated: liquidationMetadata.collateralSharesToLiquidate,
      collateralSharesToLiquidator: liquidationMetadata.collateralSharesToLiquidator
    });

    if (!params.receiveShares && liquidationMetadata.collateralSharesToLiquidator > 0) {
      vm.expectCall(
        address(vars.collateralHub),
        abi.encodeCall(
          IHubBase.remove,
          (vars.collateralAssetId, liquidationMetadata.collateralToLiquidator, params.liquidator)
        ),
        1
      );
    }

    vm.expectCall(
      address(vars.debtHub),
      abi.encodeCall(
        IHubBase.restore,
        (vars.debtAssetId, vars.drawnAmountToRestore, vars.premiumDelta)
      ),
      1
    );

    if (
      liquidationMetadata.collateralSharesToLiquidate >
      liquidationMetadata.collateralSharesToLiquidator
    ) {
      vm.expectCall(
        address(_hub(params.spoke, params.collateralReserveId)),
        abi.encodeCall(
          IHubBase.payFeeShares,
          (
            vars.collateralAssetId,
            liquidationMetadata.collateralSharesToLiquidate -
              liquidationMetadata.collateralSharesToLiquidator
          )
        )
      );
    }

    {
      for (uint256 i = params.spoke.getReserveCount(); i != 0; ) {
        i--;
        uint256 reserveId = i;
        if (_isBorrowing(params.spoke, reserveId, params.user)) {
          vars.userReservePosition = params.spoke.getUserPosition(reserveId, params.user);
          uint256 assetId = _reserveAssetId(params.spoke, reserveId);

          if (reserveId == params.debtReserveId) {
            vars.userReservePosition.drawnShares -= liquidationMetadata
              .drawnSharesToLiquidate
              .toUint120();
            if (vars.userReservePosition.drawnShares == 0) {
              continue;
            }
            vars.userReservePosition.premiumShares = uint256(vars.userReservePosition.premiumShares)
              .add(vars.premiumDelta.sharesDelta)
              .toUint120();
            vars.userReservePosition.premiumOffsetRay = (vars.userReservePosition.premiumOffsetRay +
              vars.premiumDelta.offsetRayDelta).toInt200();
          }

          IHub targetHub = _hub(params.spoke, reserveId);
          uint256 userReserveDrawnDebt = targetHub.previewRestoreByShares(
            assetId,
            vars.userReservePosition.drawnShares
          );

          if (liquidationMetadata.hasDeficit) {
            uint256 premiumDebtRay = _calculatePremiumDebtRay(
              targetHub,
              assetId,
              vars.userReservePosition.premiumShares,
              vars.userReservePosition.premiumOffsetRay
            );

            IHubBase.PremiumDelta memory premiumDelta = _getExpectedPremiumDelta({
              hub: targetHub,
              assetId: assetId,
              oldPremiumShares: vars.userReservePosition.premiumShares,
              oldPremiumOffsetRay: vars.userReservePosition.premiumOffsetRay,
              drawnShares: 0, // risk premium is 0
              riskPremium: 0,
              restoredPremiumRay: premiumDebtRay
            });

            vm.expectCall(
              address(targetHub),
              abi.encodeCall(IHubBase.reportDeficit, (assetId, userReserveDrawnDebt, premiumDelta)),
              1
            );
            vm.expectEmit(address(params.spoke));
            emit ISpoke.ReportDeficit({
              reserveId: reserveId,
              user: params.user,
              drawnShares: targetHub
                .previewRestoreByAssets(assetId, userReserveDrawnDebt)
                .toUint120(),
              premiumDelta: premiumDelta
            });
          } else {
            IHubBase.PremiumDelta memory premiumDelta = _getExpectedPremiumDelta({
              hub: targetHub,
              assetId: assetId,
              oldPremiumShares: vars.userReservePosition.premiumShares,
              oldPremiumOffsetRay: vars.userReservePosition.premiumOffsetRay,
              drawnShares: vars.userReservePosition.drawnShares,
              riskPremium: liquidationMetadata.expectedUserAccountData.riskPremium,
              restoredPremiumRay: 0
            });

            vm.expectCall(
              address(targetHub),
              abi.encodeCall(IHubBase.refreshPremium, (assetId, premiumDelta)),
              1
            );
            vm.expectEmit(address(params.spoke));
            emit ISpoke.RefreshPremiumDebt({
              reserveId: reserveId,
              user: params.user,
              premiumDelta: premiumDelta
            });
          }
        }
      }

      if (!liquidationMetadata.hasDeficit) {
        vm.expectEmit(address(params.spoke));
        emit ISpoke.UpdateUserRiskPremium({
          user: params.user,
          riskPremium: liquidationMetadata.expectedUserAccountData.riskPremium
        });
      }
    }
  }

  function _getBalanceInfo(
    ISpoke spoke,
    address addr,
    uint256 collateralReserveId,
    uint256 debtReserveId
  ) internal virtual returns (BalanceInfo memory) {
    return
      BalanceInfo({
        collateralErc20Balance: getAssetUnderlyingByReserveId(spoke, collateralReserveId).balanceOf(
          addr
        ),
        suppliedInSpoke: spoke.getUserSuppliedAssets(collateralReserveId, addr),
        addedInHub: _hub(spoke, collateralReserveId).getSpokeAddedAssets(
          _reserveAssetId(spoke, collateralReserveId),
          addr
        ),
        debtErc20Balance: getAssetUnderlyingByReserveId(spoke, debtReserveId).balanceOf(addr),
        borrowedFromSpoke: spoke.getUserTotalDebt(debtReserveId, addr),
        drawnFromHub: _hub(spoke, debtReserveId).getSpokeTotalOwed(
          _reserveAssetId(spoke, debtReserveId),
          addr
        )
      });
  }

  function _getAccountsInfo(
    CheckedLiquidationCallParams memory params
  ) internal virtual returns (AccountsInfo memory) {
    return
      AccountsInfo({
        userAccountData: params.spoke.getUserAccountData(params.user),
        userBalanceInfo: _getBalanceInfo(
          params.spoke,
          params.user,
          params.collateralReserveId,
          params.debtReserveId
        ),
        collateralHubBalanceInfo: _getBalanceInfo(
          params.spoke,
          address(_hub(params.spoke, params.collateralReserveId)),
          params.collateralReserveId,
          params.debtReserveId
        ),
        debtHubBalanceInfo: _getBalanceInfo(
          params.spoke,
          address(_hub(params.spoke, params.debtReserveId)),
          params.collateralReserveId,
          params.debtReserveId
        ),
        liquidatorBalanceInfo: _getBalanceInfo(
          params.spoke,
          params.liquidator,
          params.collateralReserveId,
          params.debtReserveId
        ),
        collateralFeeReceiverBalanceInfo: _getBalanceInfo(
          params.spoke,
          _getFeeReceiver(params.spoke, params.collateralReserveId),
          params.collateralReserveId,
          params.debtReserveId
        ),
        debtFeeReceiverBalanceInfo: _getBalanceInfo(
          params.spoke,
          _getFeeReceiver(params.spoke, params.debtReserveId),
          params.collateralReserveId,
          params.debtReserveId
        ),
        spokeBalanceInfo: _getBalanceInfo(
          params.spoke,
          address(params.spoke),
          params.collateralReserveId,
          params.debtReserveId
        ),
        userLastRiskPremium: params.spoke.getUserLastRiskPremium(params.user)
      });
  }

  function _getLiquidationMetadata(
    CheckedLiquidationCallParams memory params,
    ISpoke.UserAccountData memory userAccountDataBefore
  ) internal virtual returns (LiquidationMetadata memory) {
    uint256 debtToTarget = liquidationLogicWrapper.calculateDebtToTargetHealthFactor(
      _getCalculateDebtToTargetHealthFactorParams(
        params.spoke,
        params.collateralReserveId,
        params.debtReserveId,
        params.user
      )
    );

    LiquidationLogic.LiquidationAmounts memory liquidationAmounts = liquidationLogicWrapper
      .calculateLiquidationAmounts(
        _getCalculateLiquidationAmountsParams(
          params.spoke,
          params.collateralReserveId,
          params.debtReserveId,
          params.user,
          params.debtToCover
        )
      );

    uint256 liquidationBonus = params.spoke.getLiquidationBonus(
      params.collateralReserveId,
      params.user,
      userAccountDataBefore.healthFactor
    );

    bool isCollateralAffectingUserHf;
    {
      uint256 effectiveLiquidationBonusWad = _calculateEffectiveLiquidationBonusWad(
        params,
        liquidationAmounts
      );

      assertApproxEqRel(
        effectiveLiquidationBonusWad,
        liquidationBonus.bpsToWad(),
        _approxRelFromBps(10),
        'effective liquidation bonus should be approx equal to liquidation bonus'
      );

      // health factor is decreasing due to liquidation bonus / collateral factor if:
      //   lb * cf > hf_beforeLiq
      isCollateralAffectingUserHf =
        effectiveLiquidationBonusWad.percentMulUp(
          _getCollateralFactor(params.spoke, params.collateralReserveId, params.user)
        ) > userAccountDataBefore.healthFactor;
    }

    bool hasDeficit = (userAccountDataBefore.activeCollateralCount == 1) &&
      (!params.isSolvent || isCollateralAffectingUserHf) &&
      (liquidationAmounts.collateralSharesToLiquidate ==
        params.spoke.getUserPosition(params.collateralReserveId, params.user).suppliedShares);

    ISpoke.UserAccountData memory expectedUserAccountData = _calculateExpectedUserAccountData(
      CalculateExpectedUserAccountDataParams({
        spoke: params.spoke,
        user: params.user,
        collateralReserveId: params.collateralReserveId,
        debtReserveId: params.debtReserveId,
        collateralSharesToLiquidate: liquidationAmounts.collateralSharesToLiquidate,
        drawnSharesToLiquidate: liquidationAmounts.drawnSharesToLiquidate,
        premiumDebtRayToLiquidate: liquidationAmounts.premiumDebtRayToLiquidate,
        hasDeficit: hasDeficit
      })
    );

    uint256 debtToLiquidate = _hub(params.spoke, params.debtReserveId).previewRestoreByShares(
      _reserveAssetId(params.spoke, params.debtReserveId),
      liquidationAmounts.drawnSharesToLiquidate
    ) + liquidationAmounts.premiumDebtRayToLiquidate.fromRayUp();
    IHubBase collateralHub = _hub(params.spoke, params.collateralReserveId);
    uint256 collateralAssetId = _reserveAssetId(params.spoke, params.collateralReserveId);
    uint256 userDrawnShares = _getUserDrawnShares(params.spoke, params.debtReserveId, params.user);

    return
      LiquidationMetadata({
        debtToTarget: debtToTarget,
        collateralToLiquidate: collateralHub.previewRemoveByShares(
          collateralAssetId,
          liquidationAmounts.collateralSharesToLiquidate
        ),
        collateralToLiquidator: collateralHub.previewRemoveByShares(
          collateralAssetId,
          liquidationAmounts.collateralSharesToLiquidator
        ),
        collateralSharesToLiquidate: liquidationAmounts.collateralSharesToLiquidate,
        collateralSharesToLiquidator: liquidationAmounts.collateralSharesToLiquidator,
        debtToLiquidate: debtToLiquidate,
        drawnSharesToLiquidate: liquidationAmounts.drawnSharesToLiquidate,
        premiumDebtRayToLiquidate: liquidationAmounts.premiumDebtRayToLiquidate,
        liquidationBonus: liquidationBonus,
        expectedUserAccountData: expectedUserAccountData,
        fullDebtReserveLiquidated: liquidationAmounts.drawnSharesToLiquidate == userDrawnShares,
        isCollateralAffectingUserHf: isCollateralAffectingUserHf,
        hasDeficit: hasDeficit
      });
  }

  function _calculateEffectiveLiquidationBonusWad(
    CheckedLiquidationCallParams memory params,
    LiquidationLogic.LiquidationAmounts memory liquidationAmounts
  ) internal view returns (uint256) {
    uint256 collateralValueRemoved;
    uint256 debtValueRepaid;

    // collateral reserve
    {
      uint256 collateralBefore = params.spoke.getUserSuppliedAssets(
        params.collateralReserveId,
        params.user
      );
      uint256 collateralAfter = _hub(params.spoke, params.collateralReserveId)
        .previewRemoveByShares(
          _reserveAssetId(params.spoke, params.collateralReserveId),
          params.spoke.getUserPosition(params.collateralReserveId, params.user).suppliedShares -
            liquidationAmounts.collateralSharesToLiquidate
        );
      collateralValueRemoved = _convertAmountToValue(
        params.spoke,
        params.collateralReserveId,
        collateralBefore - collateralAfter
      );
    }

    // debt reserve
    {
      uint256 debtBefore = params.spoke.getUserTotalDebt(params.debtReserveId, params.user);
      uint256 drawnSharesBefore = _getUserDrawnShares(
        params.spoke,
        params.debtReserveId,
        params.user
      );
      uint256 premiumDebtRayBefore = params.spoke.getUserPremiumDebtRay(
        params.debtReserveId,
        params.user
      );
      uint256 debtAfter = _hub(params.spoke, params.debtReserveId).previewRestoreByShares(
        _reserveAssetId(params.spoke, params.debtReserveId),
        drawnSharesBefore - liquidationAmounts.drawnSharesToLiquidate
      ) + (premiumDebtRayBefore - liquidationAmounts.premiumDebtRayToLiquidate).fromRayUp();
      debtValueRepaid = _convertAmountToValue(
        params.spoke,
        params.debtReserveId,
        debtBefore - debtAfter
      );
    }

    if (collateralValueRemoved < debtValueRepaid) {
      return WadRayMath.WAD;
    }

    return collateralValueRemoved.wadDivUp(debtValueRepaid);
  }

  function _checkPositionStatus(
    CheckedLiquidationCallParams memory params,
    LiquidationMetadata memory liquidationMetadata
  ) internal virtual {
    assertEq(
      _isUsingAsCollateral(params.spoke, params.collateralReserveId, params.user),
      true,
      'user position status: using as collateral'
    );
    bool isBorrowing = _isBorrowing(params.spoke, params.debtReserveId, params.user);
    assertTrue(
      !liquidationMetadata.fullDebtReserveLiquidated
        ? (isBorrowing || liquidationMetadata.hasDeficit)
        : !isBorrowing,
      'user position status: borrowing'
    );
  }

  function _checkHealthFactor(
    CheckedLiquidationCallParams memory params,
    AccountsInfo memory accountsInfoBefore,
    AccountsInfo memory accountsInfoAfter,
    LiquidationMetadata memory liquidationMetadata
  ) internal virtual {
    if (
      accountsInfoAfter.userAccountData.totalDebtValue == 0 ||
      (params.isSolvent && !liquidationMetadata.isCollateralAffectingUserHf)
    ) {
      assertGe(
        accountsInfoAfter.userAccountData.healthFactor,
        accountsInfoBefore.userAccountData.healthFactor,
        'health factor should increase after liquidation'
      );
    } else {
      assertLe(
        accountsInfoAfter.userAccountData.healthFactor,
        accountsInfoBefore.userAccountData.healthFactor,
        'health factor should decrease after liquidation'
      );
    }

    if (accountsInfoAfter.userAccountData.totalDebtValue == 0) {
      assertEq(
        accountsInfoAfter.userAccountData.healthFactor,
        UINT256_MAX,
        'health factor should be max if all debt is liquidated'
      );
    } else if (liquidationMetadata.debtToLiquidate == liquidationMetadata.debtToTarget) {
      assertApproxEqRel(
        accountsInfoAfter.userAccountData.healthFactor,
        _getTargetHealthFactor(params.spoke),
        _approxRelFromBps(1),
        'health factor should be approx equal to target health factor'
      );
    } else if (liquidationMetadata.debtToLiquidate > liquidationMetadata.debtToTarget) {
      // dust adjusted
      assertGe(
        accountsInfoAfter.userAccountData.healthFactor,
        _getTargetHealthFactor(params.spoke),
        'health factor should be greater than or equal to target health factor'
      );
    } else {
      assertLe(
        accountsInfoAfter.userAccountData.healthFactor,
        _getTargetHealthFactor(params.spoke),
        'health factor should be less than or equal to target health factor'
      );
    }
  }

  function _checkErc20Balances(
    CheckedLiquidationCallParams memory params,
    AccountsInfo memory accountsInfoBefore,
    AccountsInfo memory accountsInfoAfter,
    LiquidationMetadata memory liquidationMetadata
  ) internal view {
    // Hubs/liquidator balances check
    if (params.receiveShares) {
      _checkErc20BalancesReceiveShares(
        params,
        accountsInfoBefore,
        accountsInfoAfter,
        liquidationMetadata
      );
    } else {
      _checkErc20BalancesReceiveAssets(
        params,
        accountsInfoBefore,
        accountsInfoAfter,
        liquidationMetadata
      );
    }

    // User
    assertEq(
      accountsInfoAfter.userBalanceInfo.collateralErc20Balance,
      accountsInfoBefore.userBalanceInfo.collateralErc20Balance,
      'user: collateral erc20 balance'
    );
    assertEq(
      accountsInfoAfter.userBalanceInfo.debtErc20Balance,
      accountsInfoBefore.userBalanceInfo.debtErc20Balance,
      'user: debt erc20 balance'
    );

    // Fee Receivers
    assertEq(
      accountsInfoAfter.collateralFeeReceiverBalanceInfo.collateralErc20Balance,
      accountsInfoBefore.collateralFeeReceiverBalanceInfo.collateralErc20Balance,
      'collateral fee receiver: collateral erc20 balance'
    );
    assertEq(
      accountsInfoAfter.collateralFeeReceiverBalanceInfo.debtErc20Balance,
      accountsInfoBefore.collateralFeeReceiverBalanceInfo.debtErc20Balance,
      'collateral fee receiver: debt erc20 balance'
    );
    assertEq(
      accountsInfoAfter.debtFeeReceiverBalanceInfo.collateralErc20Balance,
      accountsInfoBefore.debtFeeReceiverBalanceInfo.collateralErc20Balance,
      'debt fee receiver: collateral erc20 balance'
    );
    assertEq(
      accountsInfoAfter.debtFeeReceiverBalanceInfo.debtErc20Balance,
      accountsInfoBefore.debtFeeReceiverBalanceInfo.debtErc20Balance,
      'debt fee receiver: debt erc20 balance'
    );

    // Spoke
    assertEq(
      accountsInfoAfter.spokeBalanceInfo.collateralErc20Balance,
      accountsInfoBefore.spokeBalanceInfo.collateralErc20Balance,
      'spoke: collateral erc20 balance'
    );
    assertEq(
      accountsInfoAfter.spokeBalanceInfo.debtErc20Balance,
      accountsInfoBefore.spokeBalanceInfo.debtErc20Balance,
      'spoke: debt erc20 balance'
    );
  }

  function _checkErc20BalancesReceiveShares(
    CheckedLiquidationCallParams memory params,
    AccountsInfo memory accountsInfoBefore,
    AccountsInfo memory accountsInfoAfter,
    LiquidationMetadata memory liquidationMetadata
  ) internal view {
    // Hubs
    address collateralHub = address(_hub(params.spoke, params.collateralReserveId));
    address debtHub = address(_hub(params.spoke, params.debtReserveId));
    if (collateralHub == debtHub && params.collateralReserveId == params.debtReserveId) {
      assertEq(
        accountsInfoAfter.collateralHubBalanceInfo.collateralErc20Balance,
        accountsInfoBefore.collateralHubBalanceInfo.collateralErc20Balance +
          liquidationMetadata.debtToLiquidate,
        'collateral hub: collateral erc20 balance'
      );
    } else {
      assertEq(
        accountsInfoAfter.collateralHubBalanceInfo.collateralErc20Balance,
        accountsInfoBefore.collateralHubBalanceInfo.collateralErc20Balance,
        'collateral hub: collateral erc20 balance'
      );
      if (collateralHub != debtHub) {
        assertEq(
          accountsInfoAfter.debtHubBalanceInfo.collateralErc20Balance,
          accountsInfoBefore.debtHubBalanceInfo.collateralErc20Balance,
          'debt hub: collateral erc20 balance'
        );
      }
      assertEq(
        accountsInfoAfter.debtHubBalanceInfo.debtErc20Balance,
        accountsInfoBefore.debtHubBalanceInfo.debtErc20Balance +
          liquidationMetadata.debtToLiquidate,
        'debt hub: debt erc20 balance'
      );
      if (collateralHub != debtHub) {
        assertEq(
          accountsInfoAfter.collateralHubBalanceInfo.debtErc20Balance,
          accountsInfoBefore.collateralHubBalanceInfo.debtErc20Balance,
          'collateral hub: debt erc20 balance'
        );
      }
    }

    // Liquidator
    if (
      getAssetUnderlyingByReserveId(params.spoke, params.collateralReserveId) ==
      getAssetUnderlyingByReserveId(params.spoke, params.debtReserveId)
    ) {
      assertEq(
        accountsInfoAfter.liquidatorBalanceInfo.collateralErc20Balance,
        accountsInfoBefore.liquidatorBalanceInfo.collateralErc20Balance -
          liquidationMetadata.debtToLiquidate,
        'liquidator: collateral erc20 balance'
      );
    } else {
      assertEq(
        accountsInfoAfter.liquidatorBalanceInfo.collateralErc20Balance,
        accountsInfoBefore.liquidatorBalanceInfo.collateralErc20Balance,
        'liquidator: collateral erc20 balance'
      );
      assertEq(
        accountsInfoAfter.liquidatorBalanceInfo.debtErc20Balance,
        accountsInfoBefore.liquidatorBalanceInfo.debtErc20Balance -
          liquidationMetadata.debtToLiquidate,
        'liquidator: debt erc20 balance'
      );
    }
  }

  function _checkErc20BalancesReceiveAssets(
    CheckedLiquidationCallParams memory params,
    AccountsInfo memory accountsInfoBefore,
    AccountsInfo memory accountsInfoAfter,
    LiquidationMetadata memory liquidationMetadata
  ) internal view {
    // Hubs
    address collateralHub = address(_hub(params.spoke, params.collateralReserveId));
    address debtHub = address(_hub(params.spoke, params.debtReserveId));
    if (collateralHub == debtHub && params.collateralReserveId == params.debtReserveId) {
      assertEq(
        accountsInfoAfter.collateralHubBalanceInfo.collateralErc20Balance,
        accountsInfoBefore.collateralHubBalanceInfo.collateralErc20Balance -
          liquidationMetadata.collateralToLiquidator +
          liquidationMetadata.debtToLiquidate,
        'collateral hub: collateral erc20 balance'
      );
    } else {
      assertEq(
        accountsInfoAfter.collateralHubBalanceInfo.collateralErc20Balance,
        accountsInfoBefore.collateralHubBalanceInfo.collateralErc20Balance -
          liquidationMetadata.collateralToLiquidator,
        'collateral hub: collateral erc20 balance'
      );
      if (collateralHub != debtHub) {
        assertEq(
          accountsInfoAfter.debtHubBalanceInfo.collateralErc20Balance,
          accountsInfoBefore.debtHubBalanceInfo.collateralErc20Balance,
          'debt hub: collateral erc20 balance'
        );
      }

      assertEq(
        accountsInfoAfter.debtHubBalanceInfo.debtErc20Balance,
        accountsInfoBefore.debtHubBalanceInfo.debtErc20Balance +
          liquidationMetadata.debtToLiquidate,
        'debt hub: debt erc20 balance'
      );
      if (collateralHub != debtHub) {
        assertEq(
          accountsInfoAfter.collateralHubBalanceInfo.debtErc20Balance,
          accountsInfoBefore.collateralHubBalanceInfo.debtErc20Balance,
          'collateral hub: debt erc20 balance'
        );
      }
    }

    // Liquidator
    if (
      getAssetUnderlyingByReserveId(params.spoke, params.collateralReserveId) ==
      getAssetUnderlyingByReserveId(params.spoke, params.debtReserveId)
    ) {
      assertEq(
        accountsInfoAfter.liquidatorBalanceInfo.collateralErc20Balance,
        accountsInfoBefore.liquidatorBalanceInfo.collateralErc20Balance +
          liquidationMetadata.collateralToLiquidator -
          liquidationMetadata.debtToLiquidate,
        'liquidator: collateral erc20 balance'
      );
    } else {
      assertEq(
        accountsInfoAfter.liquidatorBalanceInfo.collateralErc20Balance,
        accountsInfoBefore.liquidatorBalanceInfo.collateralErc20Balance +
          liquidationMetadata.collateralToLiquidator,
        'liquidator: collateral erc20 balance'
      );
      assertEq(
        accountsInfoAfter.liquidatorBalanceInfo.debtErc20Balance,
        accountsInfoBefore.liquidatorBalanceInfo.debtErc20Balance -
          liquidationMetadata.debtToLiquidate,
        'liquidator: debt erc20 balance'
      );
    }
  }

  function _checkSpokeBalances(
    CheckedLiquidationCallParams memory params,
    AccountsInfo memory accountsInfoBefore,
    AccountsInfo memory accountsInfoAfter,
    LiquidationMetadata memory liquidationMetadata
  ) internal pure {
    // User
    assertApproxEqRel(
      accountsInfoAfter.userBalanceInfo.suppliedInSpoke,
      accountsInfoBefore.userBalanceInfo.suppliedInSpoke -
        liquidationMetadata.collateralToLiquidate,
      _approxRelFromBps(1),
      'user: collateral supplied'
    );
    assertApproxEqRel(
      accountsInfoAfter.userBalanceInfo.borrowedFromSpoke,
      (liquidationMetadata.hasDeficit)
        ? 0
        : accountsInfoBefore.userBalanceInfo.borrowedFromSpoke -
          liquidationMetadata.debtToLiquidate,
      _approxRelFromBps(1),
      'user: debt borrowed'
    );

    // Hubs
    assertEq(
      accountsInfoAfter.collateralHubBalanceInfo.suppliedInSpoke,
      accountsInfoBefore.collateralHubBalanceInfo.suppliedInSpoke,
      'collateral hub: collateral supplied'
    );
    assertEq(
      accountsInfoAfter.collateralHubBalanceInfo.borrowedFromSpoke,
      accountsInfoBefore.collateralHubBalanceInfo.borrowedFromSpoke,
      'collateral hub: debt borrowed'
    );
    assertEq(
      accountsInfoAfter.debtHubBalanceInfo.suppliedInSpoke,
      accountsInfoBefore.debtHubBalanceInfo.suppliedInSpoke,
      'debt hub: collateral supplied'
    );
    assertEq(
      accountsInfoAfter.debtHubBalanceInfo.borrowedFromSpoke,
      accountsInfoBefore.debtHubBalanceInfo.borrowedFromSpoke,
      'debt hub: debt borrowed'
    );

    // Liquidator
    if (!params.receiveShares) {
      assertEq(
        accountsInfoAfter.liquidatorBalanceInfo.suppliedInSpoke,
        accountsInfoBefore.liquidatorBalanceInfo.suppliedInSpoke,
        'liquidator: collateral supplied'
      );
    } else {
      // collateral rounded down on receiveShares, can differ by 2 wei in asset terms
      assertApproxEqAbs(
        accountsInfoAfter.liquidatorBalanceInfo.suppliedInSpoke,
        accountsInfoBefore.liquidatorBalanceInfo.suppliedInSpoke +
          liquidationMetadata.collateralToLiquidator,
        2,
        'liquidator: collateral supplied (receiveShares)'
      );
    }
    assertEq(
      accountsInfoAfter.liquidatorBalanceInfo.borrowedFromSpoke,
      accountsInfoBefore.liquidatorBalanceInfo.borrowedFromSpoke,
      'liquidator: debt borrowed'
    );

    // Fee Receivers
    assertEq(
      accountsInfoAfter.collateralFeeReceiverBalanceInfo.suppliedInSpoke,
      accountsInfoBefore.collateralFeeReceiverBalanceInfo.suppliedInSpoke,
      'collateral fee receiver: collateral supplied'
    );

    assertEq(
      accountsInfoAfter.collateralFeeReceiverBalanceInfo.borrowedFromSpoke,
      accountsInfoBefore.collateralFeeReceiverBalanceInfo.borrowedFromSpoke,
      'collateral fee receiver: debt borrowed'
    );
    assertEq(
      accountsInfoAfter.debtFeeReceiverBalanceInfo.suppliedInSpoke,
      accountsInfoBefore.debtFeeReceiverBalanceInfo.suppliedInSpoke,
      'debt fee receiver: collateral supplied'
    );
    assertEq(
      accountsInfoAfter.debtFeeReceiverBalanceInfo.borrowedFromSpoke,
      accountsInfoBefore.debtFeeReceiverBalanceInfo.borrowedFromSpoke,
      'debt fee receiver: debt borrowed'
    );

    // Spoke
    assertEq(
      accountsInfoAfter.spokeBalanceInfo.suppliedInSpoke,
      accountsInfoBefore.spokeBalanceInfo.suppliedInSpoke,
      'spoke: collateral supplied'
    );
    assertEq(
      accountsInfoAfter.spokeBalanceInfo.borrowedFromSpoke,
      accountsInfoBefore.spokeBalanceInfo.borrowedFromSpoke,
      'spoke: debt borrowed'
    );
  }

  function _checkHubBalances(
    CheckedLiquidationCallParams memory params,
    AccountsInfo memory accountsInfoBefore,
    AccountsInfo memory accountsInfoAfter,
    LiquidationMetadata memory liquidationMetadata
  ) internal view {
    // User
    assertEq(
      accountsInfoAfter.userBalanceInfo.addedInHub,
      accountsInfoBefore.userBalanceInfo.addedInHub,
      'user: added'
    );
    assertEq(
      accountsInfoAfter.userBalanceInfo.drawnFromHub,
      accountsInfoBefore.userBalanceInfo.drawnFromHub,
      'user: drawn'
    );

    // Hubs
    assertEq(
      accountsInfoAfter.collateralHubBalanceInfo.addedInHub,
      accountsInfoBefore.collateralHubBalanceInfo.addedInHub,
      'collateral hub: added'
    );
    assertEq(
      accountsInfoAfter.collateralHubBalanceInfo.drawnFromHub,
      accountsInfoBefore.collateralHubBalanceInfo.drawnFromHub,
      'collateral hub: drawn'
    );
    assertEq(
      accountsInfoAfter.debtHubBalanceInfo.addedInHub,
      accountsInfoBefore.debtHubBalanceInfo.addedInHub,
      'debt hub: added'
    );
    assertEq(
      accountsInfoAfter.debtHubBalanceInfo.drawnFromHub,
      accountsInfoBefore.debtHubBalanceInfo.drawnFromHub,
      'debt hub: drawn'
    );

    // Liquidator
    assertEq(
      accountsInfoAfter.liquidatorBalanceInfo.addedInHub,
      accountsInfoBefore.liquidatorBalanceInfo.addedInHub,
      'liquidator: added'
    );
    assertEq(
      accountsInfoAfter.liquidatorBalanceInfo.drawnFromHub,
      accountsInfoBefore.liquidatorBalanceInfo.drawnFromHub,
      'liquidator: drawn'
    );

    // Fee Receivers
    assertEq(
      accountsInfoAfter.collateralFeeReceiverBalanceInfo.drawnFromHub,
      accountsInfoBefore.collateralFeeReceiverBalanceInfo.drawnFromHub,
      'collateral fee receiver: drawn'
    );
    if (!params.receiveShares) {
      assertApproxEqRel(
        accountsInfoAfter.collateralFeeReceiverBalanceInfo.addedInHub,
        accountsInfoBefore.collateralFeeReceiverBalanceInfo.addedInHub +
          liquidationMetadata.collateralToLiquidate -
          liquidationMetadata.collateralToLiquidator,
        _approxRelFromBps(1),
        'collateral fee receiver: added'
      );
    } else {
      assertApproxEqAbs(
        accountsInfoAfter.collateralFeeReceiverBalanceInfo.addedInHub,
        accountsInfoBefore.collateralFeeReceiverBalanceInfo.addedInHub +
          liquidationMetadata.collateralToLiquidate -
          liquidationMetadata.collateralToLiquidator,
        2,
        'collateral fee receiver: added (receiveShares)'
      );
    }

    if (
      _getFeeReceiver(params.spoke, params.collateralReserveId) !=
      _getFeeReceiver(params.spoke, params.debtReserveId)
    ) {
      assertEq(
        accountsInfoAfter.debtFeeReceiverBalanceInfo.addedInHub,
        accountsInfoBefore.debtFeeReceiverBalanceInfo.addedInHub,
        'debt fee receiver: added'
      );
      assertEq(
        accountsInfoAfter.debtFeeReceiverBalanceInfo.drawnFromHub,
        accountsInfoBefore.debtFeeReceiverBalanceInfo.drawnFromHub,
        'debt fee receiver: drawn'
      );
    }

    // Spoke
    assertApproxEqRel(
      accountsInfoAfter.spokeBalanceInfo.addedInHub,
      accountsInfoBefore.spokeBalanceInfo.addedInHub - liquidationMetadata.collateralToLiquidate,
      _approxRelFromBps(10),
      'spoke: added'
    );
    assertApproxEqRel(
      accountsInfoAfter.spokeBalanceInfo.drawnFromHub,
      (liquidationMetadata.hasDeficit)
        ? 0
        : accountsInfoBefore.spokeBalanceInfo.drawnFromHub - liquidationMetadata.debtToLiquidate,
      _approxRelFromBps(1),
      'spoke: drawn'
    );
  }

  function _checkTransferSharesCall(
    CheckedLiquidationCallParams memory params,
    LiquidationMetadata memory liquidationMetadata,
    Vm.Log[] memory logs
  ) internal view {
    uint256 transferSharesEventCount = 0;
    for (uint256 i = 0; i < logs.length; i++) {
      if (logs[i].topics[0] == IHubBase.TransferShares.selector) {
        transferSharesEventCount += 1;

        assertEq(
          uint256(logs[i].topics[1]),
          _reserveAssetId(params.spoke, params.collateralReserveId)
        );
        address sender = address(uint160(uint256(logs[i].topics[2])));
        address receiver = address(uint160(uint256(logs[i].topics[3])));
        uint256 shares = abi.decode(logs[i].data, (uint256));
        uint256 expectedShares = _hub(params.spoke, params.collateralReserveId)
          .previewRemoveByAssets(
            _reserveAssetId(params.spoke, params.collateralReserveId),
            liquidationMetadata.collateralToLiquidate - liquidationMetadata.collateralToLiquidator
          );
        assertApproxEqAbs(shares, expectedShares, 1);
        assertEq(sender, address(params.spoke));
        assertEq(receiver, _getFeeReceiver(params.spoke, params.collateralReserveId));
      }
    }

    uint256 expectedTransferSharesEventCount = 0;
    if (
      !params.receiveShares &&
      liquidationMetadata.collateralToLiquidate > liquidationMetadata.collateralToLiquidator
    ) {
      expectedTransferSharesEventCount = 1;
    } else if (
      params.receiveShares &&
      liquidationMetadata.collateralSharesToLiquidate >
        liquidationMetadata.collateralSharesToLiquidator
    ) {
      expectedTransferSharesEventCount = 1;
    }

    assertEq(
      transferSharesEventCount,
      expectedTransferSharesEventCount,
      'transfer shares: event emitted'
    );
  }

  function _checkUserAccountData(
    CheckedLiquidationCallParams memory params,
    AccountsInfo memory accountsInfoAfter,
    LiquidationMetadata memory liquidationMetadata
  ) internal view {
    assertEq(accountsInfoAfter.userAccountData, liquidationMetadata.expectedUserAccountData);

    for (uint256 reserveId = 0; reserveId < params.spoke.getReserveCount(); reserveId++) {
      if (_isBorrowing(params.spoke, reserveId, params.user)) {
        ISpoke.UserPosition memory userPosition = params.spoke.getUserPosition(
          reserveId,
          params.user
        );
        assertNotEq(userPosition.drawnShares, 0, 'borrowed reserve should have non zero base debt');
        assertEq(
          userPosition.premiumShares,
          userPosition.drawnShares.percentMulUp(accountsInfoAfter.userLastRiskPremium),
          string.concat('last user risk premium in reserve ', vm.toString(reserveId))
        );
      }
    }

    assertEq(
      accountsInfoAfter.userAccountData.riskPremium,
      accountsInfoAfter.userLastRiskPremium,
      'user risk premium: user account data'
    );
    if (liquidationMetadata.hasDeficit) {
      assertEq(accountsInfoAfter.userLastRiskPremium, 0, 'user risk premium: 0 in deficit');
    }
  }

  function _execBeforeLiquidation(CheckedLiquidationCallParams memory params) internal virtual {}

  function _assertBeforeLiquidation(
    CheckedLiquidationCallParams memory params,
    AccountsInfo memory accountsInfoBefore,
    LiquidationMetadata memory liquidationMetadata
  ) internal virtual {}

  function _checkedLiquidationCall(CheckedLiquidationCallParams memory params) internal virtual {
    // make sure there is enough liquidity to liquidate
    _openSupplyPosition(params.spoke, params.collateralReserveId, MAX_SUPPLY_AMOUNT);

    _execBeforeLiquidation(params);

    AccountsInfo memory accountsInfoBefore = _getAccountsInfo(params);
    LiquidationMetadata memory liquidationMetadata = _getLiquidationMetadata(
      params,
      accountsInfoBefore.userAccountData
    );

    _assertBeforeLiquidation(params, accountsInfoBefore, liquidationMetadata);

    _expectEventsAndCalls(params, accountsInfoBefore, liquidationMetadata);
    vm.recordLogs();
    vm.prank(params.liquidator);
    params.spoke.liquidationCall(
      params.collateralReserveId,
      params.debtReserveId,
      params.user,
      params.debtToCover,
      params.receiveShares
    );
    Vm.Log[] memory logs = vm.getRecordedLogs();

    AccountsInfo memory accountsInfoAfter = _getAccountsInfo(params);

    _checkTransferSharesCall(params, liquidationMetadata, logs);
    _checkUserAccountData(params, accountsInfoAfter, liquidationMetadata);

    _checkPositionStatus(params, liquidationMetadata);
    _checkHealthFactor(params, accountsInfoBefore, accountsInfoAfter, liquidationMetadata);
    _checkErc20Balances(params, accountsInfoBefore, accountsInfoAfter, liquidationMetadata);
    _checkSpokeBalances(params, accountsInfoBefore, accountsInfoAfter, liquidationMetadata);
    _checkHubBalances(params, accountsInfoBefore, accountsInfoAfter, liquidationMetadata);
    _assertHubLiquidity(
      _hub(params.spoke, params.collateralReserveId),
      params.collateralReserveId,
      'spoke1.liquidationCall'
    );
    _assertHubLiquidity(
      _hub(params.spoke, params.debtReserveId),
      params.debtReserveId,
      'spoke1.liquidationCall'
    );
  }
}
